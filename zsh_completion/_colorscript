#compdef colorscript

_colorscript() {
    local -a commands
    local -a scripts
    local context state state_descr line
    typeset -A opt_args

    commands=(
        {-h,--help,help}':Print help information'
        {-l,--list,list}':List all installed color scripts'
        {-r,--random,random}':Run a random color script'
        {-e,--exec,exec}':Run a specified color script'
    )

    # Get list of available scripts
    if [ -d "/opt/shell-color-scripts/colorscripts" ]; then
        scripts=(${(f)"$(ls /opt/shell-color-scripts/colorscripts)"})
    fi

    _arguments -C \
        "1: :{_describe 'commands' commands}" \
        "*::arg:->args"

    case $state in
        args)
            case ${words[1]} in
                -e|--exec|exec)
                    _values 'color scripts' $scripts
                    ;;
            esac
            ;;
    esac
}

compdef _colorscript colorscript
